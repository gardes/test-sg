archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.tumour_resection_margins.v1

concept
	[at0000]	-- Surgical resection margins
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"17/05/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Details of surgical resection margin involvement of malignant tumours. May be used to describe scenarios where tumour is present at the resection margin,detailing the extent of involvement, or where tumour is absent from the margin, to indicate the distance of tumour from that margin.">
			use = <"Normally used within a histopathology archetype.">
			keywords = <"malignancy", "margins", "tumour", "cancer", "resection", "histopathology">
			misuse = <"">
			copyright = <"copyright (c) 2009 openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"">
		["MD5-CAM-1.0.1"] = <"B0049FACEA48B97AD72C789B49E8A2DC">
	>

definition
	CLUSTER[at0000] matches {	-- Surgical resection margins
		items cardinality matches {0..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Tumour presence at resection margins
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0014, 	-- Present
							at0015, 	-- Absent
							at0016, 	-- Indeterminate
							at0017]	-- Equivocal
						}
					}
				}
			}
			CLUSTER[at0006] occurrences matches {0..1} matches {	-- Details of tumour presence
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0007] occurrences matches {0..1} matches {	-- Maximum linear extent of involvement
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::122]>
								list = <
									["1"] = <
										units = <"mm">
										magnitude = <|>=0.0|>
									>
								>
							>
						}
					}
					allow_archetype CLUSTER[at0012] occurrences matches {0..*} matches {	-- Location 
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location-precise\.v1/}
					}
				}
			}
			CLUSTER[at0008] occurrences matches {0..1} matches {	-- Details of tumour absence
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Distance from closest resection margin
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::122]>
								list = <
									["1"] = <
										units = <"mm">
										magnitude = <|>=0.0|>
									>
								>
							>
						}
					}
					allow_archetype CLUSTER[at0013] occurrences matches {0..*} matches {	-- Location  
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location-precise\.v1/}
					}
				}
			}
			ELEMENT[at0010] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Surgical resection margins">
					description = <"Details of tumour involvement at margins of surgical resections/biopsies.
">
				>
				["at0001"] = <
					text = <"Tumour presence at resection margins">
					description = <"Evidence of tumour at the surgical resection margins.">
				>
				["at0006"] = <
					text = <"Details of tumour presence">
					description = <"Details of tumour present at surgical resection margins.">
				>
				["at0007"] = <
					text = <"Maximum linear extent of involvement">
					description = <"When tumour is present at surgical resection margin, the maximal length of involvement.">
				>
				["at0008"] = <
					text = <"Details of tumour absence">
					description = <"Details of surgical resection margins when tumour is absent.">
				>
				["at0009"] = <
					text = <"Distance from closest resection margin">
					description = <"When tumour is absent, the distance from tumour to the closest surgical resection margin.">
				>
				["at0010"] = <
					text = <"Description">
					description = <"General comment on tumour involvement at surgical resection margins.">
				>
				["at0012"] = <
					text = <"Location ">
					description = <"Specific anatomical details of tumour involvement at surgical resection margins.">
				>
				["at0013"] = <
					text = <"Location  ">
					description = <"Detailed anatomical localisation of tumour which is absent from the surgical resection margin.">
				>
				["at0014"] = <
					text = <"Present">
					description = <"There is evidence of tumour at surgical resection margins.">
				>
				["at0015"] = <
					text = <"Absent">
					description = <"There is no evidence of tumour at surgical resection margins.">
				>
				["at0016"] = <
					text = <"Indeterminate">
					description = <"Evidence of tumour at surgical resection margins has not been determined">
				>
				["at0017"] = <
					text = <"Equivocal">
					description = <"Evidence of tumour at the resection margin is equivocal">
				>
			>
		>
	>
