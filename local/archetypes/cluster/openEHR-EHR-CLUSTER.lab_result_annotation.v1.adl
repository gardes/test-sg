archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.lab_result_annotation.v1

concept
	[at0000]	-- Laboratory result annotation
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"08/12/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record comments and other per-node annotations within a lab_test archetype.">
			use = <"To be used within a lab_test archetype.">
			keywords = <"lab test", "comment">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Heath Frankel, Ocean Informatics, Australia", "Heather Leslie, Ocean Informatics, Australia">
	other_details = <
		["references"] = <"NEHTA, Australia. Pathology Episode v1.0 - Data Group library [Internet]. 2009;[cited 2009 Dec 8 ] Available from: http://www.nehta.gov.au/data-group-library/data-specifications/dgl-pathology

IHE. IHE, Laboratory Technical Framework, Volume 3 Content [Internet]. 2008;Available from: http://www.ihe.net/Technical_Framework/upload/ihe_lab_TF_rel2_1-Vol-3_FT_2008-08-08.pdf">
		["MD5-CAM-1.0.1"] = <"C8026FB6456E4FEE169CF9911D774400">
	>

definition
	CLUSTER[at0000] matches {	-- Laboratory result annotation
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Result name
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0001]}		-- Result Name Values
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Result comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Unexpected result indicator
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0002]}		-- Abnormal Result Indicator Values
					}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Testing method
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0003]}		-- Testing Method Values
					}
				}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {	-- Result status
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0008, 	-- Interim
							at0009, 	-- Final
							at0010, 	-- Supplementary
							at0011, 	-- Corrected (amended)
							at0012, 	-- Aborted
							at0013]	-- Never performed
						}
					}
				}
			}
			ELEMENT[at0005] occurrences matches {0..1} matches {	-- Reference range guidance
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0006] occurrences matches {0..*} matches {	-- Specimen detail
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.specimen(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0015] occurrences matches {0..1} matches {	-- Laboratory result identifier
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {	-- Laboratory details
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0017] occurrences matches {0..1} matches {	-- Link to original lab test
				value matches {
					DV_EHR_URI matches {*}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Laboratory result annotation">
					description = <"To record comments and other per-node annotations within a lab_test archetype.">
				>
				["at0001"] = <
					text = <"Unexpected result indicator">
					description = <"Indicates the degree of diagnostic significance associated with a pathology test result based on all the available clinical information.">
					no_exception = <"false">
					dc_id = <"11027">
				>
				["at0002"] = <
					text = <"Result comment">
					description = <"A comment on an individual lab result.">
					dc_id = <"11026">
				>
				["at0003"] = <
					text = <"Testing method">
					description = <"Description of the method use for an individual result.">
					no_exception = <"false">
					dc_id = <"11025">
				>
				["at0004"] = <
					text = <"Result status">
					description = <"The status of an individual lab result.">
					dc_id = <"11029">
				>
				["at0005"] = <
					text = <"Reference range guidance">
					description = <"Further advice on the applicability of the reference range.">
					dc_id = <"16158">
				>
				["at0006"] = <
					text = <"Specimen detail">
					description = <"Details of the specimen for a particular reb result.">
				>
				["at0007"] = <
					text = <"Result name">
					description = <"Name of the result to which this annotation refers. This will often be coded with a reference terminology such as LOINC.">
					no_exception = <"false">
					dc_id = <"11022">
				>
				["at0008"] = <
					text = <"Interim">
					description = <"This is an initial or interim result - further updates are anticipated.">
				>
				["at0009"] = <
					text = <"Final">
					description = <"This is the final result. No further alterations are anticipated.">
				>
				["at0010"] = <
					text = <"Supplementary">
					description = <"This is a supplememtary result in addition to the Interim result.">
				>
				["at0011"] = <
					text = <"Corrected (amended)">
					description = <"This is a Correction which should replace any previous results.">
				>
				["at0012"] = <
					text = <"Aborted">
					description = <"The testing/ reporting process was aborted and never completed.">
				>
				["at0013"] = <
					text = <"Never performed">
					description = <"The test was never performed.">
				>
				["at0014"] = <
					text = <"Laboratory details">
					description = <"Demographic details of the laboratory with responsibility for the individual result. Details of secondary laboratories may also be included.">
				>
				["at0015"] = <
					text = <"Laboratory result identifier">
					description = <"The identifier given to the laboratory test result of a pathology 
investigation.">
					dc_id = <"11018">
				>
				["at0017"] = <
					text = <"Link to original lab test">
					description = <"If the result was originally reported in  a separate lab test entry, link to that entry.">
				>
			>
		>
	>
	constraint_definitions = <
		["en"] = <
			items = <
				["ac0001"] = <
					text = <"Result Name Values">
					description = <"The term given to a single result element of a pathology test.  Can refer to a single pathology test result or to one component of a result group; e.g.  Urine Sodium Measurement.">
				>
				["ac0002"] = <
					text = <"Abnormal Result Indicator Values">
					description = <"Indicates the degree of diagnostically significant abnormality of the pathology test result based on all available clinical information (including but not limited to the reference range).">
				>
				["ac0003"] = <
					text = <"Testing Method Values">
					description = <"A description of the specific method(s) used by the laboratory to perform the analyses and produce the results for the requested pathology test(s).The method used has a critical impact in the comparability of results.  A decision on diagnosis can be affected by the method used based on likelihood of false or true positives and negatives related to sensitivities and specificities of tests.">
				>
			>
		>
	>
