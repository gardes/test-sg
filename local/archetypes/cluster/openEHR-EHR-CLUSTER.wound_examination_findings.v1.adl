archetype (adl_version=1.4; uid=7b710fa9-f7f5-4494-84c4-6b0d4d6c55d5)
	openEHR-EHR-CLUSTER.wound_examination_findings.v1

concept
	[at0000]	-- Wound examination findings
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Ed Schwab">
		["organisation"] = <"Alberta Health Services, Canada">
		["email"] = <"ed.schwab@ahs.ca">
		["date"] = <"2018-02-28">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a description plus or minus a clinical interpretation an identified wound.">
			use = <"Use to record a narrative description or a clinical interpretation an identified wound. 

Use to provide a framework in which CLUSTER archetypes can be nested to record additional structured physical examination findings. 

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			keywords = <"Wound", ...>
			misuse = <"Not to be used for recording data not related to the physical examination of an identified wound.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation, Alberta Health Services (Canada)">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway", "Heather Leslie, Ocean Informatics, Australia", "Katrina Simpson-Pineda, Alberta Health Services, Canada">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"Alberta Health Services (Canada)">
		["references"] = <"Derived from:Examination of a wound, Draft archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2018-02-28]. Available from: http://openehr.org/ckm/#showArchetype_1013.1.2107">
		["current_contact"] = <"FoundationalKnowledge@ahs.ca">
		["original_namespace"] = <"ca.ahs">
		["original_publisher"] = <"Alberta Health Services (Canada)">
		["custodian_namespace"] = <"ca.ahs">
		["MD5-CAM-1.0.1"] = <"958D0D7773FC6D6695F5C4E9836C1B05">
		["build_uid"] = <"1c51266b-d1ce-4499-84f6-39a954c70dca">
		["revision"] = <"1.0.0">
	>

definition
	CLUSTER[at0000] matches {	-- Wound examination findings
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Wound type
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0013] occurrences matches {0..1} matches {	-- Wound history
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {	-- Alias
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Body site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0011] occurrences matches {0..*} matches {	-- Structured body site
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_clock(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Examination findings
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.dimensions(-[a-zA-Z0-9_]+)*\.v1/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Wound examination findings">
					description = <"Findings observed during the physical examination of an identified wound.">
				>
				["at0001"] = <
					text = <"Body site">
					description = <"Identification of the body site under examination.">
					comment = <"The body site could be on an area of skin or a mucous membrane, for example the right knee, palate or duodenum. If the body site has previously been identified in another archetype in which this archetype is nested, this data element becomes redundant.">
				>
				["at0002"] = <
					text = <"Wound type">
					description = <"Identification of the type of wound found on physical examination.">
					comment = <"For example, abrasion, laceration, or ulcer. ">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the physical examination.">
				>
				["at0004"] = <
					text = <"Examination findings">
					description = <"Structured details about the physical examination findings.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the physical examination findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the physical examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical examination findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
					comment = <"'Wound' should be coded with a terminology, where possible.">
				>
				["at0011"] = <
					text = <"Structured body site">
					description = <"A structured anatomical location of the finding.">
					comment = <"If the body site has previously been identified in another archetype in which this archetype is then nested, this SLOT becomes redundant.">
				>
				["at0012"] = <
					text = <"Alias">
					description = <"Identification, by using a alias name, of the wound to distinguish one wound from similar wounds at the same body site.">
					comment = <"For example, 'Abrasion #1' as marked on a diagram of the body site.">
				>
				["at0013"] = <
					text = <"Wound history">
					description = <"A description of the known history of a wound.">
				>
			>
		>
	>
