archetype (adl_version=1.4; uid=1.2.36.1.2001.1001.100.97.3.2)
	openEHR-EHR-ACTION.medication.v3

concept
	[at0000]	-- Medication Action
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["organisation"] = <"National E-Health Transition Authority">
		["email"] = <"clinicalinfo@nehta.gov.au">
		["date"] = <"09-Nov-2010">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"
The recording of activities undertaken with regard to a medicine, vaccine or other therapeutic good and linking to the instruction if appropriate.
">
			use = <"
For recording the planning, issuing of a prescription, dispensing, administration, cessation, suspension, completion of a medicine, vaccine or other therapeutic good . This will usually be in response to a medication order but may be administered immediately without an order at times, thus requiring recording of the administration alone (e.g. in an emergency situation). Such a record may be made to indicate the administration of a dose, dispensing of a certain quantity or as a record of ceasing a medication. The state of the medication instruction is altered by the action taken as indicated in the Pathway definition.

There is a date and time at which this action took place (as there is for all actions) and use of this DCM indicates that some action has actually occurred.
">
			keywords = <"medication", "prescibe", "dispense", "administration", "cessation", "therapeutic good", "supply">
			misuse = <"
Use when recording an instruction or order (use Medication Instruction DCM).
">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"Published">
	other_contributors = <"Jacquie Garton-Smith, Royal Perth Hospital and DoHWA, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Jude Michel, Queensland Health, Australia", "Chris Mitchell, RACGP, Australia", "Jodie Pycroft, Adelaide Northern Division of General Practice Ltd, Australia", "Gordon Tomes, Australian Institute of Health and Welfare, Australia", "Richard Townley-O'Neill, NEHTA, Australia (Editor)", "Kylie Young, The Royal Australian College of General Practitioners, Australia">
	other_details = <
		["MD5-CAM-1.0.1"] = <"A2D34B49212773AE360E472875C96A5E">
	>

definition
	ACTION[at0000] matches {	-- Medication Action
		description matches {
			ITEM_TREE[at0017] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0020] matches {	-- Medicine
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0033] occurrences matches {0..*} matches {	-- Instructions to Subject of Care or Carer
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0023] occurrences matches {0..*} matches {	-- Ingredients and Form
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.chemical_description(-[a-zA-Z0-9_]+)*\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0021] occurrences matches {0..*} matches {	-- Reason
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0022] occurrences matches {0..1} matches {	-- Quantity of Medication
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.amount(-[a-zA-Z0-9_]+)*\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0024] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0025] occurrences matches {0..1} matches {	-- Sequence Number
						value matches {
							DV_COUNT matches {*}
						}
					}
					allow_archetype CLUSTER[at0027] occurrences matches {0..*} matches {	-- Administration
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.medication_admin(-[a-zA-Z0-9_]+)*\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0036] occurrences matches {0..1} matches {	-- Brand Substituted
						value matches {
							DV_BOOLEAN matches {
								value matches {True, False}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0030] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0026] occurrences matches {0..1} matches {	-- Batchid
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0040] occurrences matches {0..1} matches {	-- Date of Expiry
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0028] occurrences matches {0..1} matches {	-- Dispensed to
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.participation(-[a-zA-Z0-9_]+)*\.v3/}
						exclude
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0037] occurrences matches {0..1} matches {	-- Number of Times Dispensed
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0032] occurrences matches {0..1} matches {	-- Remaining Repeats
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0038] occurrences matches {0..1} matches {	-- Claim Category
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0029] occurrences matches {0..1} matches {	-- Administrative Item Code
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0031] occurrences matches {0..1} matches {	-- Administrative Manufacturer Code
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}

ontology
	terminologies_available = <"NCTIS-OID", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Medication Action">
					description = <"Details of use, administration, dispensing or other care step relating to a medicine, vaccine or other therapeutic good which may arise from an instruction from a clinician.">
					comment = <"The specification of each constituent data element is the same whether it is being used in the context of prescribed, dispensed, administered or reviewed. There may be seperate data instances for each of these contexts.">
					detail_docref = <"data_groups/NEHTA-16210-Medication_Action-Data_Group.xml">
					spec_id = <"97">
				>
				["at0017"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0020"] = <
					text = <"Medicine">
					description = <"The medicine, vaccine or other therapeutic good which was the focus of the action.">
					comment = <"It is preferred that this item be coded from the Medicines Terminology Value Domain.

This includes medications and medical devices. It includes drugs, appliances, dressings and reagents.

Identifies a therapeutic good, which is broadly defined as a good which is represented in any way to be, or is likely to be taken to be, for therapeutic use (unless specifically excluded or included under Section 7 of the Therapeutic Goods Act 1989). Therapeutic use means use in or in connection with: preventing, diagnosing, curing or alleviating a disease, ailment, defect or injury; influencing, inhibiting or modifying a physiological process; testing the susceptibility of persons to a disease or ailment; influencing, controlling or preventing conception; testing for pregnancy; or replacement or modification of parts of the anatomy. From . The formal definition of a therapeutic good (from the Therapeutic Goods Act 1989) can be found at: .

Examples:  (Some examples of AMT ConceptID and their AMT Preferred Term are:)
  1. 293049011000036110, paracetamol 500 mg + codeine phosphate 30 mg tablet
  2. 327004011000036118, paracetamol 500 mg + codeine phosphate 30 mg tablet, 20
  3. 234184011000036115, Panadeine Forte tablet: uncoated, 20 tablets
  4. 192727011000036112, Panadeine Forte (paracetamol 500 mg + codeine phosphate 30 mg) tablet: uncoated, 1 tablet
  5. 278453011000036118, Panadeine Forte tablet: uncoated, 20 tablets, blister pack
  6. 315236011000036113, bandage compression 10 cm x 3.5 m bandage: high stretch, 1 bandage
  7. 186324011000036116, Eloflex (2480) (bandage compression 10 cm x 3.5 m) bandage: high stretch, 1 bandage
  8. 73875011000036101, Je-Vax (Japanese encephalitis virus inactivated vaccine) injection: powder for, vial">
					fsn = <"Therapeutic Good Identification">
					detail_docref = <"data_elements/NEHTA-10194-Therapeutic_Good_Identification-Data_Element.xml">
					coding_preferred = <"true">
					value_domain_name = <"Medicines Terminology">
					value_domain_dc_id = <"16115">
				>
				["at0021"] = <
					text = <"Reason">
					description = <"The reason(s) the specific action or step was carried out.">
					comment = <"It is preferred that this item be coded from the Reason for Action Values Value Domain.

This is not the reason for the medication instruction, rather the specific reason e.g. for administration or for ceasing the medication.">
					fsn = <"Reason for Action">
					detail_docref = <"data_elements/NEHTA-16492-Reason_for_Action-Data_Element.xml">
					coding_preferred = <"true">
					value_domain_name = <"Reason for Action Values">
					value_domain_dc_id = <"16492">
				>
				["at0022"] = <
					text = <"Quantity of Medication">
					description = <"The quantity of medicine, vaccine or other therapeutic good.">
					detail_docref = <"data_groups/NEHTA-16423-Quantity_of_Medication-Data_Group.xml">
					spec_id = <"117">
				>
				["at0023"] = <
					text = <"Ingredients and Form">
					description = <"Detailed information about the ingredient(s) including form and strength.">
					detail_docref = <"data_groups/NEHTA-16408-Ingredients_and_Form-Data_Group.xml">
					spec_id = <"112">
				>
				["at0024"] = <
					text = <"Comment">
					description = <"A comment on the action taken.">
					fsn = <"Medication Action Comment">
					detail_docref = <"data_elements/NEHTA-16181-Medication_Action_Comment-Data_Element.xml">
				>
				["at0025"] = <
					text = <"Sequence Number">
					description = <"The sequence number specific to the action being recorded.">
					comment = <"Used to specify the sequence number of the dispensing (in prescription with repeats) or medication administration action.">
					detail_docref = <"data_elements/NEHTA-16424-Sequence_Number-Data_Element.xml">
				>
				["at0026"] = <
					text = <"Batchid">
					description = <"Assigned by the manufacturer to identify the manufacturing batch of the item.">
					fsn = <"Batch Identifier">
					detail_docref = <"data_elements/NEHTA-16273-Batch_Identifier-Data_Element.xml">
				>
				["at0027"] = <
					text = <"Administration">
					description = <"Details about the administration of the medicine, vaccine or other therapeutic good.">
					detail_docref = <"data_groups/NEHTA-10108-Administration-Data_Group.xml">
					spec_id = <"115">
				>
				["at0028"] = <
					text = <"Dispensed to">
					description = <"The name of the person to whom this was dispensed, if not the subject of care.">
					detail_docref = <"data_groups/NEHTA-10296-Dispensed_to-Data_Group.xml">
					spec_id = <"28">
				>
				["at0029"] = <
					text = <"Administrative Item Code">
					description = <"Administrative code of the pharmaceutical item supplied.">
					comment = <"It is preferred that this item be coded from the Administrative Item Code Values Value Domain.

This element is to be used to assist with claims processing.">
					detail_docref = <"data_elements/NEHTA-16646-Administrative_Item_Code-Data_Element.xml">
					coding_preferred = <"true">
					value_domain_name = <"Administrative Item Code Values">
					value_domain_dc_id = <"16645">
				>
				["at0030"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0031"] = <
					text = <"Administrative Manufacturer Code">
					description = <"Administrative code of the manufacturer of the pharmaceutical item supplied.">
					comment = <"It is preferred that this item be coded from the Administrative Manufacturer Code Values Value Domain.

This element is to be used to assist with claims processing.">
					detail_docref = <"data_elements/NEHTA-16648-Administrative_Manufacturer_Code-Data_Element.xml">
					coding_preferred = <"true">
					value_domain_name = <"Administrative Manufacturer Code Values">
					value_domain_dc_id = <"16647">
				>
				["at0032"] = <
					text = <"Remaining Repeats">
					description = <"The number of times the medicine, vaccine or other therapeutic good may still be dispensed without re-issue of a prescription or order.">
					comment = <"This is the information required by the subject of care and prescriber.">
					detail_docref = <"data_elements/NEHTA-16427-Remaining_Repeats-Data_Element.xml">
				>
				["at0033"] = <
					text = <"Instructions to Subject of Care or Carer">
					description = <"Any instructions given to the subject of care or carer at the time of the action.">
					fsn = <"Medication Action Instructions">
					detail_docref = <"data_elements/NEHTA-16491-Medication_Action_Instructions-Data_Element.xml">
				>
				["at0036"] = <
					text = <"Brand Substituted">
					description = <"A different brand of the same medicine, vaccine or other therapeutic good was substituted for the one nominated in the order.">
					comment = <"For dispense records for medicines.

Examples:  (See: .)">
					fsn = <"Brand Substitution Occurred">
					detail_docref = <"data_elements/NEHTA-16064-Brand_Substitution_Occurred-Data_Element.xml">
				>
				["at0037"] = <
					text = <"Number of Times Dispensed">
					description = <"The number of times this order has been dispensed.">
					comment = <"The sum of this number minus one and the remaining repeats provides the number of repeats on the original order.">
					detail_docref = <"data_elements/NEHTA-10114-Number_of_Times_Dispensed-Data_Element.xml">
				>
				["at0038"] = <
					text = <"Claim Category">
					description = <"The category of reimbursement or subsidy sought for the item.">
					comment = <"It is preferred that this item be coded from the Therapeutic Good Claim Category Reference Set Value Domain.

Applicable to dispense records.

The primary purpose of this data element is to enable the determination of the source of any applicable financial subsidy for the item. Not to be confused with Concession Benefit.

Examples: 
  1. General PBS benefit
  2. Safety Net Concession benefit
  3. Safety Net Entitlement Card benefit">
					detail_docref = <"data_elements/NEHTA-16060-Claim_Category-Data_Element.xml">
					coding_preferred = <"true">
					value_domain_name = <"Therapeutic Good Claim Category Reference Set">
					value_domain_dc_id = <"16060">
				>
				["at0040"] = <
					text = <"Date of Expiry">
					description = <"The expiry date as documented by the manufacturer.">
					fsn = <"Expiry Date">
					detail_docref = <"data_elements/NEHTA-16425-Expiry_Date-Data_Element.xml">
				>
			>
		>
	>
	term_bindings = <
		["NCTIS-OID"] = <
			items = <
				["at0000"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.16210]>
				["at0020"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.10194]>
				["at0021"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16492]>
				["at0022"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.16423.97.1]>
				["at0023"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.16408.97.1]>
				["at0024"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16181]>
				["at0025"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16424]>
				["at0026"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16273]>
				["at0027"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.10108.97.1]>
				["at0028"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.10296.97.1]>
				["at0029"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16646]>
				["at0031"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16648]>
				["at0032"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16427]>
				["at0033"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16491]>
				["at0036"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16064]>
				["at0037"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.10114]>
				["at0038"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16060]>
				["at0040"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16425]>
				["at0041"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.104.16115]>
				["at0048"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.104.16492]>
				["at0071"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.104.16060]>
				["at0074"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.104.16645]>
				["at0077"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.104.16647]>
				["at0080"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.10296.97.2]>
				["at0082"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.10296.97.3]>
				["at0084"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16591]>
				["at0086"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16637]>
				["at0088"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.102.16692]>
				["at0090"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16699]>
				["at0092"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16700]>
				["at0095"] = <[NCTIS-OID::1.2.36.1.2001.1001.101.103.16693]>
			>
		>
	>
