archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.body_temperature.v1

concept
	[at0000]	-- Body temperature
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2004-05-18">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the measured temperature of a person - as a surrogate for the temperature of the whole person. Also as means to record the calculated mean temperature of the body.

">
			use = <"Used for recording the whole body temperature of a person or body.  The mean body temperature is estimated by calculation

Additional clusters can be included to provide additional state data - including environmental conditions, menstrual cycle details and exertion details

Please Note: The site and method of recording may need to be displayed to the end user to facilitate accurate intrepretation of the temperature recorded.">
			keywords = <"temperature", "body", "core", "fever", "mean">
			misuse = <"This archetype is not to be used to record the temperature of any other object.

This archetype is not to be used to record the temperature of a part of the body in isolation eg temperature of the sole of the foot as a part of chronic diabetes managment.

">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Archetype Editorial Group", ...>
	other_details = <
		["references"] = <"null">
	>

definition
	OBSERVATION[at0000] matches {	-- Body temperature
		data matches {
			HISTORY[at0002] matches {	-- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0003] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0001] matches {	-- Single
								items cardinality matches {1; unordered} matches {
									ELEMENT[at0004] matches {	-- Temperature
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::127]>
												list = <
													["1"] = <
														units = <"°C">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
													["2"] = <
														units = <"°F">
														magnitude = <|>=32.0|>
														precision = <|1|>
													>
												>
											>
										}
									}
									ELEMENT[at0053] occurrences matches {0..1} matches {	-- Mean Temperature
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::127]>
												list = <
													["1"] = <
														units = <"°C">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
													["2"] = <
														units = <"°F">
														magnitude = <|>=32.0|>
														precision = <|1|>
													>
												>
											>
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0029] matches {	-- State
								items cardinality matches {0..1; ordered} matches {
									ELEMENT[at0030] occurrences matches {0..1} matches {	-- Body exposure
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0031, 	-- Naked
													at0032, 	-- Reduced clothing/bedding
													at0033, 	-- Appropriate clothing/bedding
													at0034; 	-- Increased clothing/bedding
													at0033]	-- assumed value
												}
											}
										}
									}
									CLUSTER[at0060] occurrences matches {0..1} matches {	-- Environmental conditions
										items cardinality matches {0..*; unordered} matches {
											allow_archetype CLUSTER occurrences matches {0..*} matches {
												include
													archetype_id/value matches {/environmental_conditions(-[a-zA-Z0-9_]+)*\.v1/}
											}
										}
									}
									CLUSTER[at0063] occurrences matches {0..1} matches {	-- Menstrual cycle
										items cardinality matches {0..*; unordered} matches {
											CLUSTER[at0064] occurrences matches {0..1} matches {	-- LNMP
												items cardinality matches {0..*; unordered} matches {
													allow_archetype ELEMENT occurrences matches {0..*} matches {
														include
															archetype_id/value matches {/lnmp\.v1/}
													}
												}
											}
											CLUSTER[at0061] occurrences matches {0..1} matches {	-- Days since LNMP
												items cardinality matches {0..*; unordered} matches {
													allow_archetype ELEMENT occurrences matches {0..*} matches {
														include
															archetype_id/value matches {/menstrual_cycle_day\.v1/}
													}
												}
											}
										}
									}
									CLUSTER[at0062] occurrences matches {0..1} matches {	-- Exertion
										items cardinality matches {0..*; unordered} matches {
											allow_archetype CLUSTER occurrences matches {0..*} matches {
												include
													archetype_id/value matches {/exertion(-[a-zA-Z0-9_]+)*\.v1/}
											}
										}
									}
									ELEMENT[at0041] occurrences matches {0..1} matches {	-- Description of thermal stress
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0020] matches {	-- Protocol
				items cardinality matches {0..1; ordered} matches {
					ELEMENT[at0021] occurrences matches {0..1} matches {	-- Site of measurement
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0022, 	-- Mouth
									at0023, 	-- Ear
									at0024, 	-- Axilla
									at0025, 	-- Rectum
									at0026, 	-- Nasopharynx
									at0027, 	-- Urinary bladder
									at0028, 	-- Intravascular
									at0043, 	-- Skin
									at0051, 	-- Vagina
									at0054, 	-- Oesphagus
									at0055]	-- Inguinal skin crease
								}
							}
						}
					}
					CLUSTER[at0045] occurrences matches {0..1} matches {	-- Device
						items cardinality matches {0..*; unordered} matches {
							allow_archetype CLUSTER occurrences matches {0..*} matches {
								include
									archetype_id/value matches {/device-thermometer(-[a-zA-Z0-9_]+)*\.v1/}
							}
						}
					}
				}
			}
		}
	}

ontology
	terminologies_available = <"LNC205", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Body temperature">
					description = <"A measurement of the body temperature, which is a surrogate for the whole body temperature of the person.">
				>
				["at0001"] = <
					text = <"Single">
					description = <"*">
				>
				["at0002"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Any event">
					description = <"Any event">
				>
				["at0004"] = <
					text = <"Temperature">
					description = <"The measured body temperature (as a surrogate for the whole body)">
				>
				["at0020"] = <
					text = <"Protocol">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Site of measurement">
					description = <"The anatomical site of measurement of the temperature">
				>
				["at0022"] = <
					text = <"Mouth">
					description = <"Temperature is measured within the mouth">
				>
				["at0023"] = <
					text = <"Ear">
					description = <"Temperature is measured from within the external auditory canal">
				>
				["at0024"] = <
					text = <"Axilla">
					description = <"Temperature is measured from the skin of the axilla with the arm positioned down by the side">
				>
				["at0025"] = <
					text = <"Rectum">
					description = <"Temperature measured within the rectum">
				>
				["at0026"] = <
					text = <"Nasopharynx">
					description = <"Temperature is measured within the nasopharynx">
				>
				["at0027"] = <
					text = <"Urinary bladder">
					description = <"Temperature is measured in the urinary bladder">
				>
				["at0028"] = <
					text = <"Intravascular">
					description = <"Temperature is measured within the vascular system">
				>
				["at0029"] = <
					text = <"State">
					description = <"State information about the patient">
				>
				["at0030"] = <
					text = <"Body exposure">
					description = <"The thermal situation of the person who is having the temperature taken">
				>
				["at0031"] = <
					text = <"Naked">
					description = <"No clothing, bedding or covering">
				>
				["at0032"] = <
					text = <"Reduced clothing/bedding">
					description = <"The person is covered by a lesser amount of clothing or bedding than deemed appropriate for the environmental circumstances">
				>
				["at0033"] = <
					text = <"Appropriate clothing/bedding">
					description = <"The person is covered by an amount of clothing or bedding deemed appropriate for the environmental circumstances">
				>
				["at0034"] = <
					text = <"Increased clothing/bedding">
					description = <"The person is covered by an increased amount of clothing or bedding than deemed appropriate for the environmental circumstances">
				>
				["at0041"] = <
					text = <"Description of thermal stress">
					description = <"Description of the conditions applied to the person that might influence their body temperature">
				>
				["at0043"] = <
					text = <"Skin">
					description = <"Temperature is measured from exposed skin">
				>
				["at0045"] = <
					text = <"Device">
					description = <"Device used to record the measured temperature.  Specific devices can be recorded using the nested CLUSTER.device archetype">
				>
				["at0051"] = <
					text = <"Vagina">
					description = <"Temperature is measured within the vagina">
				>
				["at0053"] = <
					text = <"Mean Temperature">
					description = <"An average body temperature, which takes into account temperature variations throughout the body, and calculated using a weighted algorithm.">
				>
				["at0054"] = <
					text = <"Oesphagus">
					description = <"Temperatue is measured within the oesophagus">
				>
				["at0055"] = <
					text = <"Inguinal skin crease">
					description = <"Temperature is measured in the inguinal skin crease between the leg and abdominal wall">
				>
				["at0060"] = <
					text = <"Environmental conditions">
					description = <"Cluster describing the environmental conditions to which the person is exposed.  Assumed temperatures should be regarded as 21 degrees Celsius or 69.8 degrees Fahrenheit. ">
				>
				["at0061"] = <
					text = <"Days since LNMP">
					description = <"Days since Last Normal Menstrual Period">
				>
				["at0062"] = <
					text = <"Exertion">
					description = <"Cluster describing the amount of work or exercise that may influence the temperature measurement.  Assumed value is at rest.">
				>
				["at0063"] = <
					text = <"Menstrual cycle">
					description = <"Details about the woman's menstrual cycle.  Assumed value is unknown">
				>
				["at0064"] = <
					text = <"LNMP">
					description = <"Date of Last Normal Menstrual Period">
				>
			>
		>
	>
	term_bindings = <
		["LNC205"] = <
			items = <
				["/data[at0002]/events[at0003]/data[at0001]/item[at0004]"] = <[LNC205::8310-5]>
				["/data[at0002]/events[at0005]/data[at0001]/item[at0004]"] = <[LNC205::8321-2]>
				["/data[at0002]/events[at0006]/data[at0001]/item[at0004]"] = <[LNC205::8311-3]>
				["/data[at0002]/events[at0007]/data[at0001]/item[at0004]"] = <[LNC205::8316-2]>
				["/data[at0002]/events[at0008]/data[at0001]/item[at0004]"] = <[LNC205::8332-0]>
				["/data[at0002]/events[at0009]/data[at0001]/item[at0004]"] = <[LNC205::8312-1]>
				["/data[at0002]/events[at0017]/data[at0001]/item[at0004]"] = <[LNC205::8325-3]>
				["/data[at0002]/events[at0019]/data[at0001]/item[at0004]"] = <[LNC205::8320-4]>
			>
		>
	>
