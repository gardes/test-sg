archetype (adl_version=1.4; uid=551982af-e9ca-4697-802a-d938d3a8020e)
	openEHR-EHR-OBSERVATION.eq_5d_5l.v0

concept
	[at0000]	-- EQ-5D-5L
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Ed Schwab">
		["organisation"] = <"Alberta Health Services, Canada">
		["email"] = <"ed.schwab@ahs.ca">
		["date"] = <"2018-05-09">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To provide a simple, generic measure of health for clinical and economic appraisal.">
			use = <"The EQ-5D-5L essentially consists of 2 pages – the EQ-5D descriptive system and the EQ visual analogue scale (EQ VAS). The EQ-5D-5L descriptive system comprises the following 5 dimensions: mobility, self-care, usual activities, pain/discomfort and anxiety/depression. Each dimension has 5 levels: no problems, slight problems, moderate problems, severe problems and extreme problems.

Scoring the EQ-5D-5L Descriptive System: The respondent is asked to indicate his/her health state by ticking (or placing a cross) in the box against the most appropriate statement in each of the 5 dimensions.

Scoring the EQ VAS: The EQ VAS records the respondent’s self-rated health on a vertical, visual analogue scale where the endpoints are labeled ‘Best imaginable health state’ and ‘Worst imaginable health state’. This information can be used as a quantitative measure of health outcome as judged by the individual respondents.

While openEHR archetypes are all freely available under an open license, the specific content of the EQ-5D-5L archetype is copyright protected. Any use of this archetype within implementations must be in compliance with the terms established by the copyright owners. 
The EQ-5D is a trademark of the EuroQol Research Foundation. Please register with APERSU to obtain a license for using this instrument: https://apersu.ca/our-services/

https://www.ncbi.nlm.nih.gov/pubmed/21479777 
https://www.ncbi.nlm.nih.gov/pubmed/26492214.">
			keywords = <"health status", "health state", "EQ-5D">
			misuse = <"It should be noted that the numerals 1-5 have no arithmetic properties and should not be used as a cardinal score.

This archetype contains the EQ-5D-5L scoring algorithm based on Canadian preferences and would not be appropriate for use in other countries.

Users of the EQ-5D-5L archetype must ensure that they comply with the terms of use of the EuroQol Research Foundation who own the copyright of the original assessment https://euroqol.org/support/how-to-obtain-eq-5d/.">
			copyright = <"© Alberta Health Services (Canada)">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Alberta Health Services (Canada)">
		["references"] = <"1. The EuroQol Group (1990). EuroQol-a new facility for the measurement of health-related quality of life. Health Policy 16(3):199-208. 
2. Brooks R (1996). EuroQol: the current state of play. Health Policy 37(1):53-72. 
3. Dolan P (1997). Modeling valuations for EuroQol health states. Med Care 35(11):1095-108. 
4. Roset M, Badia X, Mayo NE (1999). Sample size calculations in studies using the EuroQol 5D. Qual Life Res 8(6):539-49. 
5. Greiner W, Weijnen T, Nieuwenhuizen M, et al. (2003). A single European currency for EQ-5D health states. Results from a six country study. Eur J Health Econ; 4(3):222-231. 
6. Shaw JW, Johnson JA, Coons SJ (2005). US valuation of the EQ-5D health states: development and testing of the D1 valuation model. Med Care; 43(3): 203-220.
7. https://apersu.ca/
8. https://apersu.ca/about-eq-5d/">
		["current_contact"] = <"FoundationalKnowledge@ahs.ca">
		["original_namespace"] = <"ca.ahs">
		["original_publisher"] = <"Alberta Health Services (Canada)">
		["custodian_namespace"] = <"ca.ahs">
		["MD5-CAM-1.0.1"] = <"5DAA35FA557227EFEC4FE9879A087F5E">
		["build_uid"] = <"d9735ae7-89f8-420e-a200-8761b9e3dee2">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {	-- EQ-5D-5L
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Mobility
										value matches {
											1|[local::at0009], 	-- I have no problems in walking about
											2|[local::at0010], 	-- I have slight problems in walking about
											3|[local::at0040], 	-- I have moderate problems walking about
											4|[local::at0039], 	-- I have severe problems in walking about
											5|[local::at0011], 	-- I am unable to walk about
											9|[local::at0034]  	-- No response/ambiguous value
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Self-care
										value matches {
											1|[local::at0012], 	-- I have no problems washing or dressing myself
											2|[local::at0013], 	-- I have slight problems washing or dressing myself
											3|[local::at0041], 	-- I have moderate problems washing or dressing myself
											4|[local::at0042], 	-- I have severe problems washing or dressing myself
											5|[local::at0014], 	-- I am unable to wash or dress myself
											9|[local::at0035]  	-- No response/ambiguous value
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- Usual activities (e.g. work, study, housework, family or leisure activities)
										value matches {
											1|[local::at0015], 	-- I have no problems with performing my usual activities
											2|[local::at0016], 	-- I have slight problems with performing my usual activities
											3|[local::at0043], 	-- I have moderate problems doing my usual activity
											4|[local::at0044], 	-- I have severe problems doing my usual activities
											5|[local::at0017], 	-- I am unable to do my usual activities
											9|[local::at0036]  	-- No response/ambiguous value
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- Pain/Discomfort
										value matches {
											1|[local::at0018], 	-- I have no pain or discomfort
											2|[local::at0019], 	-- I have slight pain or discomfort
											3|[local::at0045], 	-- I have moderate pain or discomfort
											4|[local::at0046], 	-- I have severe pain or discomfort
											5|[local::at0020], 	-- I have extreme pain or discomfort
											9|[local::at0037]  	-- No response/ambiguous value
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {	-- Anxiety/Depression
										value matches {
											1|[local::at0021], 	-- I am not anxious or depressed
											2|[local::at0022], 	-- I am slightly anxious or depressed
											3|[local::at0047], 	-- I am moderately anxious or depressed
											4|[local::at0048], 	-- I am severely anxious or depressed
											5|[local::at0023], 	-- I am extremely anxious or depressed
											9|[local::at0038]  	-- No response/ambiguous value
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Health state
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0049] occurrences matches {0..1} matches {	-- Index value
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::380]>
												list = <
													["1"] = <
														units = <"1">
														magnitude = <|>-0.15..<1.0|>
														precision = <|3|>
													>
												>
											>
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {	-- How good or bad is your health today?
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::380]>
												list = <
													["1"] = <
														units = <"1">
														magnitude = <|0.0..100.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0026] matches {*}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"EQ-5D-5L">
					description = <"The EQ-5D in a standardized generic preference-based measure of health status that could be used in economic evaluations of health interventions">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Self-reported health status captured by EQ-5D-5L relates to the respondent’s situation at the time of completion. No attempt is made to summarise recalled health status over the preceding days or weeks, although EQ-5D-3L has been tested in recall mode. An early decision taken by the EuroQol Group determined that health status measurement ought to apply to the respondent’s immediate situation - hence the focus on ‘your health today’.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Mobility">
					description = <"Numerical dipiction of the person's self assessed ability to attend to mobile and active.">
					comment = <"Instructions to respondent:
\"Under each heading, please tick the ONE box  that best describes your health TODAY.\"">
				>
				["at0005"] = <
					text = <"Self-care">
					description = <"Numerical dipiction of the person's self assessed ability to attend to self care needs independantly.">
					comment = <"Instructions to respondent:
\"Under each heading, please tick the ONE box  that best describes your health TODAY.\"">
				>
				["at0006"] = <
					text = <"Usual activities (e.g. work, study, housework, family or leisure activities)">
					description = <"Numerical dipiction of the person's self assessed ability to attend to usual activities for example work, study, housework, family or leisure.">
					comment = <"Instructions to respondent:
\"Under each heading, please tick the ONE box  that best describes your health TODAY.\"">
				>
				["at0007"] = <
					text = <"Pain/Discomfort">
					description = <"Numerical dipiction of the person's self assessed level of pain or discomfort.">
					comment = <"Instructions to respondent:
\"Under each heading, please tick the ONE box  that best describes your health TODAY.\"">
				>
				["at0008"] = <
					text = <"Anxiety/Depression">
					description = <"Numerical dipiction of the person's self assessed anxity or depressed emotional state.">
					comment = <"Instructions to respondent:
\"Under each heading, please tick the ONE box  that best describes your health TODAY.\"">
				>
				["at0009"] = <
					text = <"I have no problems in walking about">
					description = <"Level 1: indicating no problem.">
				>
				["at0010"] = <
					text = <"I have slight problems in walking about">
					description = <"Level 2: indicating slight problems.">
				>
				["at0011"] = <
					text = <"I am unable to walk about">
					description = <"Level 3: indicating extreme problems.">
				>
				["at0012"] = <
					text = <"I have no problems washing or dressing myself">
					description = <"Level 1: indicating no problem.">
				>
				["at0013"] = <
					text = <"I have slight problems washing or dressing myself">
					description = <"Level 2: indicating slight problems.">
				>
				["at0014"] = <
					text = <"I am unable to wash or dress myself">
					description = <"Level 5: indicating extreme problems.">
				>
				["at0015"] = <
					text = <"I have no problems with performing my usual activities">
					description = <"Level 1: indicating no problem.">
				>
				["at0016"] = <
					text = <"I have slight problems with performing my usual activities">
					description = <"Level 2: indicating slight problems.">
				>
				["at0017"] = <
					text = <"I am unable to do my usual activities">
					description = <"Level 5: indicating extreme problems.">
				>
				["at0018"] = <
					text = <"I have no pain or discomfort">
					description = <"Level 1: indicating no problem.">
				>
				["at0019"] = <
					text = <"I have slight pain or discomfort">
					description = <"Level 2: indicating slight problems.">
				>
				["at0020"] = <
					text = <"I have extreme pain or discomfort">
					description = <"Level 5: indicating extreme problems.">
				>
				["at0021"] = <
					text = <"I am not anxious or depressed">
					description = <"Level 1: indicating no problem.">
				>
				["at0022"] = <
					text = <"I am slightly anxious or depressed">
					description = <"Level 2: indicating slight problems.">
				>
				["at0023"] = <
					text = <"I am extremely anxious or depressed">
					description = <"Level 5: indicating extreme problems.">
				>
				["at0024"] = <
					text = <"Health state">
					description = <"A unique health state is defined by combining 1 level from each of the 5 dimensions. Each state is referred to in terms of a 5 digit code.">
					comment = <"Example: a state of 11111 indicates no problems on any of the 5 dimensions, while state 12345 indicates no problems with mobility, slight problems with washing or dressing, moderate problems with doing usual activities, severe pain or discomfort and extreme anxiety or depression.">
				>
				["at0025"] = <
					text = <"How good or bad is your health today?">
					description = <"The visiual analogue scale records the respondents self-rated health on a vertical, visual analogue scale where the endpoints are labelled \"Best imaginable health state' and ' Worst imaginable health state'.">
					comment = <"After providing the visual analogue scale to the respondent, the EQ-5D-5L asks respondents to simply 'mark an X on the scale to indicate how your health is TODAY' and then to 'write the number you marked on the scale in the box below'.">
				>
				["at0026"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0034"] = <
					text = <"No response/ambiguous value">
					description = <"No score indicated.">
				>
				["at0035"] = <
					text = <"No response/ambiguous value">
					description = <"No score indicated.">
				>
				["at0036"] = <
					text = <"No response/ambiguous value">
					description = <"No score indicated.">
				>
				["at0037"] = <
					text = <"No response/ambiguous value">
					description = <"No score indicated.">
				>
				["at0038"] = <
					text = <"No response/ambiguous value">
					description = <"No score indicated.">
				>
				["at0039"] = <
					text = <"I have severe problems in walking about">
					description = <"Level 4: indicating sever problems.">
				>
				["at0040"] = <
					text = <"I have moderate problems walking about">
					description = <"Level 3: indicating moderate problems.">
				>
				["at0041"] = <
					text = <"I have moderate problems washing or dressing myself">
					description = <"Level 3: indicating moderate problems.">
				>
				["at0042"] = <
					text = <"I have severe problems washing or dressing myself">
					description = <"Level 4: indicating severe problems.">
				>
				["at0043"] = <
					text = <"I have moderate problems doing my usual activity">
					description = <"Level 4: indicating moderate problems.">
				>
				["at0044"] = <
					text = <"I have severe problems doing my usual activities">
					description = <"Level 4: indicating severe problems.">
				>
				["at0045"] = <
					text = <"I have moderate pain or discomfort">
					description = <"Level 3: indicating moderate problems.">
				>
				["at0046"] = <
					text = <"I have severe pain or discomfort">
					description = <"Level 4: indicating severe problems.">
				>
				["at0047"] = <
					text = <"I am moderately anxious or depressed">
					description = <"Level 3: indicating moderate problems.">
				>
				["at0048"] = <
					text = <"I am severely anxious or depressed">
					description = <"Level 4: indicating severe problems.">
				>
				["at0049"] = <
					text = <"Index value">
					description = <"EQ-5D-5L health states, defined by the EQ-5D-5L descriptive system, may be converted into a single index value. The index values, presented in country specific value sets, facilitate the calculation of quality-adjusted life years (QALYs) that are used to inform economic evaluations of health care interventions.">
					comment = <"The EQ-5D-5L scoring algorithm based on Canadian preferences is calculated as follows:
 
EQ5D_index = 1.1351 - 0.0389*MO - 0.0458*SC - 0.0195*UA -0.0444*PD - 0.0376*AD -0.0510*(MO45) - 0.0584*(SC45) - 0.1103*(UA45) - 0.1409*(PD45) - 0.1277*(AD45) + 0.0085*Num45sq  

MO is the level on the mobility dimension, range 1 - 5 
SC is the level on the self-care dimension, range 1 - 5 
UA is the level on the usual activities dimension, range 1 - 5 
PD is the level on the pain/discomfort dimension, range 1 - 5 
AD is the level on the anxiety/depression dimension, range 1 - 5 

MO45 is the number of levels 4 or 5 on the mobility dimension, range 0 - 1 (for example if someone has level 3 on mobility then MO45=0, if someone has level 4, then MO45=1)
SC45 is the number of levels 4 or 5 on the self-care dimension, range 0 - 1 
UA45 is the number of levels 4 or 5 on the usual activities dimension, range 0 - 1 
PD45 is the number of levels 4 or 5 on the pain/discomfort dimension, range 0 - 1 
AD45 is the number of levels 4 or 5 on the anxiety/depression dimension, range 0 - 1 

Num45sq: this is the number of levels 4 or 5 beyond the first 1 squared, range 0 - 4. For example, for health state 34221, Num45sq=0. For health state 34521, Num45sq=1. 
">
				>
			>
		>
	>
