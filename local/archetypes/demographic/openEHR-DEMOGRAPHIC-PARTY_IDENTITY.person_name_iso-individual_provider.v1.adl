archetype (adl_version=1.4)
	openEHR-DEMOGRAPHIC-PARTY_IDENTITY.person_name_iso-individual_provider.v1
specialise
	openEHR-DEMOGRAPHIC-PARTY_IDENTITY.person_name_iso.v1

concept
	[at0000.1]	-- Nome do profissional de saúde
language
	original_language = <[ISO_639-1::pt-br]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Rigoleta Dutra & Sergio Miranda Freire">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
		["date"] = <"20/05/2009">
	>
	details = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação do nome de uma profissional de saúde">
			use = <"Utilizado em serviços de demografia para representar os nomes que um profissional de saúde pode ter">
			keywords = <"serviço demográfico", "nome de uma profissional de saúde">
			misuse = <"">
			copyright = <"copyright (c) 2009 openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of a individual provider's name">
			use = <"Used in demographic services to represent the names that an individual health provider is known by">
			keywords = <"demographic service", "individual provider's name">
			misuse = <"">
			copyright = <"copyright (c) 2009 openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"ISO/DTS 27527:2007(E) - Provider Identification - Draft Technnical Specification - International Organization for Standardization">
	>

definition
    PARTY_IDENTITY[at0000.1] matches {  -- Nome da pessoa - Especialização: profissional de saúde
        details matches {
            ITEM_TREE[at0001] occurrences matches {0..1} matches {  -- Componentes do nome
                items cardinality matches {1..*; ordered} matches {
                    CLUSTER[at0002] occurrences matches {0..*} matches {  -- Grupo do nome atribuído
                        items cardinality matches {2..2; ordered} matches {
                            ELEMENT[at0010] occurrences matches {1..1} matches {  -- Nome atribuído
                                value matches {
                                    DV_TEXT matches {*}  
                                }
                            }
                            ELEMENT[at0011] occurrences matches {1..1} matches {  -- Número de seqüência do nome atribuído
                                value matches {
                                    DV_COUNT matches {*}   
                                }
                            }
                        }
                    }
                    CLUSTER[at0003] occurrences matches {1..*} matches {  -- Grupo do sobrenome
                        items cardinality matches {2..2; ordered} matches {
                            ELEMENT[at0012] occurrences matches {1..1} matches {  -- Nome de família
                                value matches {
                                    DV_TEXT matches {*}  
                                }
                            }
                            ELEMENT[at0013] occurrences matches {1..1} matches {  -- Número de seqüência do sobrenome
                                value matches {
                                    DV_COUNT matches {*}   
                                }
                            }
                        }
                    }
                    CLUSTER[at0004] occurrences matches {0..*} matches {  -- Grupo de título do nome
                        items cardinality matches {2..2; ordered} matches {
                            ELEMENT[at0014] occurrences matches {1..1} matches {  -- Título do nome
                                value matches {
                                    DV_CODED_TEXT matches {  
                                        defining_code matches {[ac0000]}
                                    }
                                }
                            }
                            ELEMENT[at0015] occurrences matches {1..1} matches {  -- Número de seqüência do título do nome
                                value matches {
                                    DV_COUNT matches {*}
                                }
                            }
                        }
                    }
                    CLUSTER[at0005] occurrences matches {0..*} matches {  -- Grupo de sufixo do nome
                        items cardinality matches {2..2; ordered} matches {
                            ELEMENT[at0016] occurrences matches {1..1} matches {  -- Sufixo do nome
                                value matches {
                                    DV_CODED_TEXT matches {  
                                        defining_code matches {[ac0001]}
                                    }
                                }
                            }
                            ELEMENT[at0017] occurrences matches {1..1} matches {  -- Número de seqüência do sufixo do nome
                                value matches {
                                    DV_COUNT matches {*}  
                                }
                            }
                        }
                    }
                    CLUSTER[at0006] occurrences matches {1..*} matches {  -- Grupo de utilização do nome
                        items cardinality matches {2..3; ordered} matches {
                            ELEMENT[at0018] occurrences matches {1..1} matches {  -- Uso do nome
                                value matches {
                                    DV_CODED_TEXT matches {   
                                        defining_code matches {
                                            [local::
                                            at0023,
                                            at0024,
                                            at0025,
                                            at0026,
                                            at0027,
                                            at0028]
                                        }
                                    }
                                }
                            }
                            ELEMENT[at0019] occurrences matches {1..1} matches {  -- Intervalo de utilização
                                value matches {
                                    DV_INTERVAL<DV_DATE> matches {*} 
                                }
                            }
                            ELEMENT[at0020] occurrences matches {0..1} matches {  -- Identificador usado
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                        }
                    }
                    CLUSTER[at0007] occurrences matches {0..1} matches {  -- Representação alternativa do nome
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0021] occurrences matches {1..1} matches {  -- Uso da representação
                                value matches {
                                    DV_CODED_TEXT matches {   
                                        defining_code matches {[ac0002]}
                                    }
                                }
                            }
                            ELEMENT[at0022] occurrences matches {1..1} matches {  -- Representação alternativa do nome
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                        }
                    }
                    ELEMENT[at0008] occurrences matches {1..1} matches {  -- Nome preferencial
                        value matches {
                            DV_BOOLEAN matches {*} 
                        }
                    }
                    ELEMENT[at0009.1] occurrences matches {0..0} matches {  -- Indicador de uso condicional
                        value matches {
                            DV_CODED_TEXT matches {   
                                defining_code matches {
                                    [local::
                                    at0031,
                                    at0032,
                                    at0033,
                                    at0034,
                                    at0035,
                                    at0036]
                                }
                            }
                        }
                    }
                    CLUSTER[at0.2] occurrences matches {0..*} matches {  -- Grupo de utilização restrita do nome
                        items cardinality matches {1..*; unordered; unique} matches {
                            ELEMENT[at0.3] occurrences matches {1..1} matches {  -- Tipo de restrição
                                value  matches {
                                    DV_CODED_TEXT matches  {
  		                             defining_code matches {
 		                                   [local::
                		                    at0041,
                                		    at0042,
		                                    at0043]
 		                               }

 	                             }
 	                         }
			    } 
                            ELEMENT[at0.4] occurrences matches {1..1} matches {  -- Intervalo da restrição
                                value matches {
                                    DV_INTERVAL matches {*} 
                                }
                            }
                            ELEMENT[at0.5] occurrences matches {0..*} matches {  -- Identificador do prestador
                                value matches {
                                    DV_TEXT matches {*}  
                                }
                            }
                        }
                    }
                }
            }
        }
    }


ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000.1"] = <
					text = <"Nome do profissional de saúde">
					description = <"Estrutura do nome de uma pessoa - Especialização: profissional de saúde">
				>
				["at0001"] = <
					text = <"Componentes do nome">
					description = <"Componentes do nome da pessoa">
				>
				["at0002"] = <
					text = <"Grupo do nome atribuído">
					description = <"Grupo que consiste de um nome atribuído e a ordem dele no nome">
				>
				["at0003"] = <
					text = <"Grupo do sobrenome">
					description = <"Grupo que consiste de um sobrenome e a ordem dele no nome">
				>
				["at0004"] = <
					text = <"Grupo de título do nome">
					description = <"Grupo que consiste de um título do nome de uma pessoa e a ordem dele no nome">
				>
				["at0005"] = <
					text = <"Grupo de sufixo do nome">
					description = <"Grupo que consiste de um sufixo do nome de uma pessoa e a ordem dele no nome">
				>
				["at0006"] = <
					text = <"Grupo de utilização do nome">
					description = <"Grupo que descreve como este nome deve ser usado">
				>
				["at0007"] = <
					text = <"Representação alternativa do nome">
					description = <"Grupo que descreve uma forma alternativa de representar o nome de uma pessoa">
				>
				["at0008"] = <
					text = <"Nome preferencial">
					description = <"Indica o nome pelo qual a pessoa prefere ser identificada">
				>
				["at0009.1"] = <
					text = <"Indicador de uso condicional">
					description = <"Um indicador das condições específicas ou regras que devem ser aplicadas ao nome de uma pessoa">
				>
				["at0010"] = <
					text = <"Nome atribuído">
					description = <"O nome que identifica a pessoa no grupo familiar ou pelo qual ela é univocamente identificada socialmente">
				>
				["at0011"] = <
					text = <"Número de seqüência do nome atribuído">
					description = <"Um indicator da ordem de uso para os nomes atribuídos">
				>
				["at0012"] = <
					text = <"Nome de família">
					description = <"A parte do nome que uma pessoa possui em comum com outros membros de sua família, distinta do(s) seu(s) nome(s) atribuído(s)">
				>
				["at0013"] = <
					text = <"Número de seqüência do sobrenome">
					description = <"Um indicator da ordem de uso para os sobrenomes">
				>
				["at0014"] = <
					text = <"Título do nome">
					description = <"Uma forma honorífica que inicia o nome, usada ao se referir a uma pessoa pelo nome, seja por correspondência ou por telefone, ou, dependendo da situação cultura, pessoalmente">
				>
				["at0015"] = <
					text = <"Número de seqüência do título do nome">
					description = <"Um indicator da ordem de uso para os títulos do nome">
				>
				["at0016"] = <
					text = <"Sufixo do nome">
					description = <"Termo adicional usado após o nome da pessoa">
				>
				["at0017"] = <
					text = <"Número de seqüência do sufixo do nome">
					description = <"Um indicator da ordem de uso para os sufixos do nome">
				>
				["at0018"] = <
					text = <"Uso do nome">
					description = <"Uma classificação que permite a diferenciação entre os usos do nome de uma pessoa. Um nome de um indivíduo pode ter muitos usos">
				>
				["at0019"] = <
					text = <"Intervalo de utilização">
					description = <"O período de tempo para o qual este uso do nome se aplica para o nome ao qual ele está associado">
				>
				["at0020"] = <
					text = <"Identificador usado">
					description = <"A combinação do tipo de identificador, emissor do identificador e nome do identificador que especifica o vínculo entre este nome e o uso do identificador para relatório, ou outros usos">
				>
				["at0021"] = <
					text = <"Uso da representação">
					description = <"Nome da forma de representação usada">
				>
				["at0022"] = <
					text = <"Representação alternativa do nome">
					description = <"Representação alternative do nome desta pessoa usando estilos alternativos de representação, tais como caracteres ou variações do conjunto de caracteres do idioma para exibição local">
				>
				["at0023"] = <
					text = <"Nome para relatório">
					description = <"o nome do sujeito usado para relatório, quando acompanhado por um identificador específico">
				>
				["at0024"] = <
					text = <"Nome de recém-nascido">
					description = <"tipo reservado para identificação de recém-nascidos sem nome">
				>
				["at0025"] = <
					text = <"Nome profissional ou comercial">
					description = <"o nome usado pela pessoa para propósitos profissionais ou de negócios">
				>
				["at0026"] = <
					text = <"Nome de solteiro(a)">
					description = <"o nome usado pela pessoa antes de se casar">
				>
				["at0027"] = <
					text = <"Nome legal">
					description = <"Nome registrado (legal) para a pessoa">
				>
				["at0028"] = <
					text = <"Outro nome">
					description = <"qualquer outro nome pelo qual o sujeito é conhecido, ou foi conhecido no passado">
				>
				["at0031"] = <
					text = <"Nome não confiável">
					description = <"indica que o nome registrado é um nome fictício ou parcial">
				>
				["at0032"] = <
					text = <"Escrito de forma errada">
					description = <"Este indicador permite ao usuário indicar que há um erro de digitação, mas que deve ser retido para um uso potencial na vinculação de registros">
				>
				["at0033"] = <
					text = <"Nome para não ser usado">
					description = <"Indica que este nome não deve ser usado ao se referir à pessoa">
				>
				["at0034"] = <
					text = <"Vínculo do nome proibido por lei">
					description = <"indica que este nome e todos os nomes antes dele na seqüência de nomes não devem ser exibidos ou indicados de nenhum modo ao procurar por ou lidar com informações e eventos associados com um nome que segue a este na seqüência de nomes">
				>
				["at0035"] = <
					text = <"Requisito especial seguranca/privacidade">
					description = <"indica um nome para o qual episódios são vinculados e devem somente ser acessados por pessoas autorizadas">
				>
				["at0036"] = <
					text = <"Nome temporário">
					description = <"Indica que o nome da pessoa não foi determinado">
				>
				["at0041"] = <
					text = <"Nome não confiável">
					description = <"indica que o nome registrado é um nome fictício ou parcial">
				>
				["at0042"] = <
					text = <"Nome não para uso continuado">
					description = <"Indica que este nome não deve ter um uso continuado. Certos nomes tribais podem se tornar 'para uso não continuado'">
				>
				["at0043"] = <
					text = <"Requisito especial seguranca/privacidade">
					description = <"indica um nome para o qual episódios são vinculados e devem somente ser acessados por pessoas autorizadas">
				>
				["at0.2"] = <
					text = <"Grupo de utilização restrita do nome">
					description = <"Este conjunto de dados se aplicam a um nome específico e são usados em sistemas computacionais para restringir o modo como um nome é usado ou exibido naquele sistema">
				>
				["at0.3"] = <
					text = <"Tipo de restrição">
					description = <"Um indicador de condições especiais que devem ser aplicadas ao nome do profissional">
				>
				["at0.4"] = <
					text = <"Intervalo da restrição">
					description = <"Indica o período de tempo em que esta restrição se aplica">
				>
				["at0.5"] = <
					text = <"Identificador do prestador">
					description = <"Este campo indica os prestadores que podem visualizar este nome e os detalhes associados a ele">
				>
			>
		>
		["en"] = <
			items = <
				["at0000.1"] = <
					text = <"Individual provider name">
					description = <"Person's Name structure - Specialization: individual health provider">
				>
				["at0001"] = <
					text = <"Name components">
					description = <"Components of a person's name">
				>
				["at0002"] = <
					text = <"Given Name Group">
					description = <"Group consisting of a given name and the order of the given name in the name">
				>
				["at0003"] = <
					text = <"Family Name Group">
					description = <"Group consisting of a family name and the order of the family name in the name">
				>
				["at0004"] = <
					text = <"Name Title Group">
					description = <"Group consisting of a person´s title and the order of the title in the name">
				>
				["at0005"] = <
					text = <"Name Suffix Group">
					description = <"Group consisting of a person's name suffix and the order of the suffix in the name">
				>
				["at0006"] = <
					text = <"Name Usage Group">
					description = <"Group that describes how this name should be used">
				>
				["at0007"] = <
					text = <"Alternative Name Representation">
					description = <"Group that describes alternative ways of representing a person's name">
				>
				["at0008"] = <
					text = <"Preferred Name">
					description = <"Indicates the name by which a person chooses to be identified">
				>
				["at0009.1"] = <
					text = <"Conditional Use">
					description = <"An indicator of specific conditions or rules that should be applied to a person's name">
				>
				["at0010"] = <
					text = <"Given Name">
					description = <"The person's identifying name(s) within the family group or by which he/she is uniquely socially identified">
				>
				["at0011"] = <
					text = <"Given Name Sequence Number">
					description = <"An indicator of the order of use for Given Names">
				>
				["at0012"] = <
					text = <"Family Name">
					description = <"The part of a name a person usually has in common with some other members of his/her family, as distinguished from his/her given names">
				>
				["at0013"] = <
					text = <"Family Name Sequence Number">
					description = <"An indicator of the order of use for Family Names">
				>
				["at0014"] = <
					text = <"Name Title">
					description = <"An honorific form of address commencing a name, used when addressing a person by name, whether by mail, by phone, or depending upon cultural situation in person">
				>
				["at0015"] = <
					text = <"Name Title Sequence Number">
					description = <"An indicator of the order of use for name titles">
				>
				["at0016"] = <
					text = <"Name Suffix">
					description = <"Additional term used following a person’s name">
				>
				["at0017"] = <
					text = <"Name Suffix Sequence Number">
					description = <"An indicator of the order of use for name suffix">
				>
				["at0018"] = <
					text = <"Name Usage">
					description = <"A classification that enables differentiation between the usage of names for a person. An individual name may have many name uses">
				>
				["at0019"] = <
					text = <"Usage interval">
					description = <"The period of time at which this name usage for the name to which the usage is associated applies">
				>
				["at0020"] = <
					text = <"Usage Identifier">
					description = <"The combination of identifier type, identifier issuer and identifier name that specify the link between this name and reporting or other unique identifier usage">
				>
				["at0021"] = <
					text = <"Representation usage">
					description = <"Name of the representational form used">
				>
				["at0022"] = <
					text = <"Alternative Representation">
					description = <"Alternative Representation of this subject of care name using alternative styles of representation such as characters or language character set variations for local display.">
				>
				["at0023"] = <
					text = <"Reporting Name">
					description = <"the subject’s name as it is to be used for reporting, when used with a specific identifier">
				>
				["at0024"] = <
					text = <"Newborn Name">
					description = <"type reserved for the identification of unnamed newborn babies">
				>
				["at0025"] = <
					text = <"Professional or Business Name">
					description = <"the name used by the person for business or professional purposes">
				>
				["at0026"] = <
					text = <"Maiden Name">
					description = <"the name used by the subject of care prior to marriage">
				>
				["at0027"] = <
					text = <"Registered Name (Legal Name)">
					description = <"Registered Name (Legal Name)">
				>
				["at0028"] = <
					text = <"Other Name">
					description = <"any other name that a subject is also known by, or has been known by in the past">
				>
				["at0031"] = <
					text = <"Unreliable information">
					description = <"indicates that the name recorded is a fictitious or partial name">
				>
				["at0032"] = <
					text = <"Known misspelling">
					description = <"This indicator allows the user to indicate that this is a misspelling, but that it is one that should be retained for potential future matching">
				>
				["at0033"] = <
					text = <"Name Not to be Used">
					description = <"Indicates that this name should not be used when referring to this subject">
				>
				["at0034"] = <
					text = <"Name linkage forbidden by law">
					description = <"indicates that this name and all names prior to it in name sequence are not to be displayed or indicated in any way when searching for or dealing with information and events associated with a name that is subsequent to this one in the name sequence">
				>
				["at0035"] = <
					text = <"Special Privacy/Security Requirements">
					description = <"indicate a name for which episodes are attached that should only be accessible to specified authorised persons">
				>
				["at0036"] = <
					text = <"Temporary Name">
					description = <"indicate that the person's name has not been determined">
				>
				["at0041"] = <
					text = <"Unreliable information">
					description = <"indicates that the name recorded is a fictitious or partial name">
				>
				["at0042"] = <
					text = <"Name not for continued use">
					description = <"Indicates that this name is not for continued use. Certain tribal names may become 'not for continued use'">
				>
				["at0043"] = <
					text = <"Special Privacy/Security Requirement">
					description = <"indicate a name for which episodes are attached that should only be accessible to specified authorised persons">
				>
				["at0.2"] = <
					text = <"Restricted name usage">
					description = <"This set of data elements apply to a specific name set and are used within computer systems to restrict the way a name is used or displayed in that system">
				>
				["at0.3"] = <
					text = <"Type of restriction">
					description = <"An indicator of special conditions or rules that shall be applied to an individual provider name">
				>
				["at0.4"] = <
					text = <"Restriction interval">
					description = <"Indicates the period of time where this restriction applies">
				>
				["at0.5"] = <
					text = <"Available provider">
					description = <"This field indicates those providers who should be able to see this name and the details assoociated with it">
				>
			>
		>
	>
	constraint_definitions = <
		["pt-br"] = <
			items = <
				["ac0000"] = <
					text = <"Títulos">
					description = <"Termos como Doutor, Mestre, ....">
				>
				["ac0001"] = <
					text = <"Sufixos">
					description = <"Sufixos do nome – neto, neta, junior, ...">
				>
				["ac0002"] = <
					text = <"Códigos para o idioma de representação">
					description = <"idiomas para a representação alternativa do nome">
				>
			>
		>
		["en"] = <
			items = <
				["ac0000"] = <
					text = <"Titles">
					description = <"Terms like Dr, Prof, Rev etc used as an honorific form of addressing a person">
				>
				["ac0001"] = <
					text = <"Suffixes">
					description = <"Terms like Jr, PhD used after the name">
				>
				["ac0002"] = <
					text = <"Language code">
					description = <"Set of languages used for an alternative name representation">
				>
			>
		>
	>
