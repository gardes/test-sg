archetype (adl_version=1.4)
	openEHR-DEMOGRAPHIC-ORGANISATION.healthcare_provider_organisation.v1

concept
	[at0000]	-- Healthcare organisation
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Chunlan Ma">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of a healthcare provider organisation's data.">
			use = <"Used in demographic service to collect a healthcare provider organisation's data.">
			keywords = <"demographic service", "healthcare provider organisation data">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"Authordraft">
	other_contributors = <"Heath Frankel", "Lisa Thurston">
	other_details = <
		["references"] = <"ISO/TS 22220:2008(E) - Identification of Subject of Care - Technical Specification - International Organization for Standardization.">
	>

definition
	ORGANISATION[at0000] matches {	 
		details matches {
			ITEM_TREE[at0001] matches { -- organisation identifiers
				items cardinality matches {1..*; ordered} matches {
					ELEMENT[at0010] occurrences matches {1..*} matches {	-- organisation identifier
				        value matches {
				            DV_IDENTIFIER matches {*}
			            }
			        }
					CLUSTER[at0011] occurrences matches {0..*} matches {  -- Wards
                        items cardinality matches {1..*; ordered} matches {
                            allow_archetype CLUSTER[at0012]	matches {
					            include
					                archetype_id/value matches {/openEHR-DEMOGRAPHICS-CLUSTER\.ward[a-zA-Z0-9_-]*\.v1/}
					                archetype_id/value matches {/openEHR-DEMOGRAPHICS-CLUSTER\.unit[a-zA-Z0-9_-]*\.v1/}
                            }
                        }
                    }
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Organisation type
						value matches {
							DV_CODED_TEXT matches {*}
						}
					}		
					ELEMENT[at0014] occurrences matches {0..1} matches {	-- Organisation size
						value matches {
							DV_CODED_TEXT matches {*}
						}
					}
				}
            }
        }
		identities cardinality matches {1..*; ordered} matches {
			allow_archetype PARTY_IDENTITY[at0002] occurrences matches {1..*} matches {	
				include
					archetype_id/value matches {/openEHR-DEMOGRAPHIC-PARTY_IDENTITY\.organisation_name[a-zA-Z0-9_-]*\.v1/}
			}
		}
		contacts cardinality matches {0..*; ordered} matches {
			CONTACT[at0003] matches {
				addresses cardinality matches {1..*; ordered} matches {
					allow_archetype ADDRESS[at0030] matches {
						include 
							archetype_id/value  matches {/openEHR-DEMOGRAPHIC-ADDRESS\.address[a-zA-Z0-9_-]*\.v1/}
							archetype_id/value  matches {/openEHR-DEMOGRAPHIC-ADDRESS\.electronic_communication[a-zA-Z0-9_-]*\.v1/}
					}
				}
			}
		}
		relationships cardinality matches {0..*; ordered} matches {			
			 PARTY_RELATIONSHIP[at0004] occurrences matches {0..1} matches {  -- Third-party payer relationship
                details matches {
                    ITEM_TREE[at0040] occurrences matches {0..1} matches {  -- Details
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0041] occurrences matches {1..*} matches {	-- Organisation Id issued by third party payer
				                value matches {
				                    DV_IDENTIFIER matches {*}
			                    }
			                }
                        }
                    }
                }
            }
			PARTY_RELATIONSHIP[at0005] occurrences matches {0..1} matches {  -- district relationship
				details matches {
					ITEM_TREE[at0050] occurrences matches {0..1} matches {  -- Details
						items cardinality matches {1..*; ordered} matches {
							ELEMENT[at0051] occurrences matches {1..*} matches {	-- Organisation Id or code identified within a district
								value matches {
									DV_IDENTIFIER matches {*}
								}
							}
						}
					}
				}
			}
		}
    }

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Healthcare organisation">
					description = <"Organisation demographic data">
				>
				["at0001"] = <
					text = <"Identifiers">
					description = <"Organisation identifiers">
				>
				["at0002"] = <
					text = <"Name">
					description = <"Identification - the names the organisation is known by">
				>
				["at0003"] = <
					text = <"Contact">
					description = <"organiation contacts">
				>
				["at0004"] = <
					text = <"Third-party payer relationship">
					description = <"relationship between the health provider organisation and a third-party payer">
				>
				["at0005"] = <
					text = <"District relationship">
					description = <"Relationship between two organisations or between an organization and one of its division/department, etc">
				>
				["at0010"] = <
					text = <"Organisation identifier">
					description = <"An organisation identifier">
				>
				["at0011"] = <
					text = <"Wards">
					description = <"Ward">
				>
				["at0012"] = <
					text = <"Wards">
					description = <"Wards">
				>
				["at0013"] = <
					text = <"Organisation type">
					description = <"Externally-coded text information about organisation type">
				>
				["at0014"] = <
					text = <"Organisation size">
					description = <"Externally-coded text information about organisation size">
				>
				["at0030"] = <
					text = <"Address">
					description = <"An organisation contact address">
				>
				["at0040"] = <
					text = <"Details">
					description = <"Relationship details">
				>
				["at0041"] = <
					text = <"organisation identifier issued by the third-party payer">
					description = <"Identifier used by the health provider in the relationship with the third-party payer">
				>
				["at0050"] = <
					text = <"Details">
					description = <"district Relationship details">
				>
				["at0051"] = <
					text = <"organisation identifier or code within the district relationship">
					description = <"organisation identifier within the district relationship">
				>
			>
		>
	>
