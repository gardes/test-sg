archetype (adl_version=1.4)
	openEHR-DEMOGRAPHIC-PERSON.person.v1

concept
	[at0000]	-- Person
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of a person's demographic data.">
			use = <"Used in demographic service to collect a person's data.">
			keywords = <"demographic service", "person, person data">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"Authordraft">
	other_contributors = <>
	other_details = <
		["references"] = <"">
	>

definition
    PERSON[at0000] matches {  -- Person demographic data
        details matches {
            ITEM_TREE[at0001] occurrences matches {0..1} matches {  -- Details
                items cardinality matches {0..*; ordered} matches {
                    allow_archetype CLUSTER[at0010] occurrences matches {0..*} matches {
                        include    
                            archetype_id/value matches {/(person_additional_data)[a-zA-Z0-9_-]*\.v1/}
                            archetype_id/value matches {/(person_birth_data)[a-zA-Z0-9_-]*\.v1/}
                            archetype_id/value matches {/(person_death_data)[a-zA-Z0-9_-]*\.v1/}
                            archetype_id/value matches {/(person_identifier)[a-zA-Z0-9_-]*\.v1/}
                    }
                      ELEMENT[at0011] occurrences matches {0..*} matches {	-- Person identifier
				                       value matches {
				                             	DV_IDENTIFIER matches {*}
			                       	}
                        }
                }
            }
        }
        identities cardinality matches {1..*; ordered} matches {
            allow_archetype PARTY_IDENTITY[at0002] occurrences matches {1..1} matches {
                include
                    archetype_id/value matches {/(person_name)[a-zA-Z0-9_-]*\.v1/}
            }
        }
        contacts cardinality matches {0..*; ordered} matches {
            CONTACT[at0003] occurrences matches {1..1} matches {  -- Contacts
                addresses cardinality matches {1..*; ordered} matches {
                    allow_archetype ADDRESS[at0030] occurrences matches {1..1} matches {
                        include
                            archetype_id/value matches {/(address)([a-zA-Z0-9_]+)*\.v1/}
                            archetype_id/value matches {/(electronic_communication)[a-zA-Z0-9_-]*\.v1/}
                    }
                }
            }
        }
        relationships cardinality matches {0..*; ordered} matches {
            allow_archetype PARTY_RELATIONSHIP[at0020] occurrences matches {1..1} matches {  -- relationships
                        include
                            archetype_id/value matches {/.*/}
            }
        }
   }

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Person">
					description = <"Person demographic data">
				>
				["at0001"] = <
					text = <"Demographic details">
					description = <"A person's demographic details.">
				>
				["at0002"] = <
					text = <"Name">
					description = <"A person's name.">
				>
				["at0003"] = <
					text = <"Contacts">
					description = <"A person's contacts.">
				>
				["at0020"] = <
					text = <"Relationships">
					description = <"A person's relationships, especially family ties.">
				>
				["at0010"] = <
					text = <"Birth, death, documentation and other demographic data">
					description = <"Allows the collection of data about birth and death data, documentation and other person's demographic data.">
				>
				["at0011"] = <
					text = <"Person identifier">
					description = <"Person unique identifier">
				>
				["at0030"] = <
					text = <"Addresses">
					description = <"Addresses linked to a single contact, i.e., with the same time validity.">
				>
			>
		>
	>
