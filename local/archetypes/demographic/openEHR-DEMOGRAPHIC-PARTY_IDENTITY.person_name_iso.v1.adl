archetype (adl_version=1.4)
	openEHR-DEMOGRAPHIC-PARTY_IDENTITY.person_name_iso.v1

concept
	[at0000]	-- Nome da pessoa
language
	original_language = <[ISO_639-1::pt-br]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Rigoleta Dutra & Sergio Miranda Freire">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
		["date"] = <"20/05/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of a personal name, based on ISO/TS 22220 standard.">
			use = <"Used to represent the names by which a person is known.">
			keywords = <"demographic service", "person name">
			misuse = <"">
			copyright = <"copyright (c) 2009 openEHR Foundation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação do nome de uma pessoa com base na norma ISO/TS 22220.">
			use = <"Utilizado para representar os nomes que uma pessoa pode ter.">
			keywords = <"serviço demográfico", "nome de uma pessoa">
			misuse = <"">
			copyright = <"copyright (c) 2009 openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Rong Chen, Cambio Healthcare Systems, Sweden", "Ricardo Correia, SBIM/FMUP, Portugal", "Grahame Grieve, Australia">
	other_details = <
		["references"] = <"ISO/TS 22220:2008(E) - Identification of Subject of Care - Technical Specification- International Organization for Standardization.">
	>

definition
    PARTY_IDENTITY[at0000] matches {  -- Person name
        details matches {
            ITEM_TREE[at0001] occurrences matches {0..1} matches {  -- name components
                items cardinality matches {1..*; ordered} matches {
                    CLUSTER[at0002] occurrences matches {0..1} matches {  -- Given Names 
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0011] occurrences matches {1..*} matches {  -- Given Name
                                value matches {
                                    DV_TEXT matches {*}  
                                }
                            }
                        }
                    }
                    CLUSTER[at0003] occurrences matches {1} matches {  -- Family Names 
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0012] occurrences matches {1..*} matches {  -- Family Name
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                        }
                    }
                    CLUSTER[at0004] occurrences matches {0..1} matches {  -- Title Group
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0014] occurrences matches {1..*} matches {  -- Name Title
                                value matches {
                                    DV_TEXT matches {*}
                                }
                            }
                        }
                    }
                    CLUSTER[at0005] occurrences matches {0..*} matches {  -- Suffix group
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0016] occurrences matches {1..*} matches {  -- Name Suffix
                                value matches {
                                    DV_TEXT matches {*}
                                }
                            }
                         }
                    }
                    CLUSTER[at0006] occurrences matches {1..*} matches {  -- Name Usage Group
                        items cardinality matches {2..3; ordered} matches {
                            ELEMENT[at0018] occurrences matches {1..1} matches {  -- Name Usage
                                value matches {
				    DV_TEXT matches {*}
                                    DV_CODED_TEXT matches {   
                                        defining_code matches {
                                            [local::
                                            at0023,
                                            at0024,
                                            at0025,
                                            at0026,
                                            at0027,
                                            at0028]
                                        }
                                    }
                                }
                            }
                            ELEMENT[at0019] occurrences matches {1..1} matches {  -- Usage Interval
                                value matches {
                                    DV_INTERVAL<DV_DATE> matches {*}  
                                }
                            }
                            ELEMENT[at0020] occurrences matches {0..1} matches {  -- usage Identifier
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                        }
                    }
                    CLUSTER[at0007] occurrences matches {0..1} matches {  -- Alternative Name Representation
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0021] occurrences matches {1..1} matches {  -- Representation usage
                                value matches {
                                    DV_TEXT matches {*}
                                }
                            }
                            ELEMENT[at0022] occurrences matches {1..1} matches {  -- Alternative representation
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                        }
                    }
                    ELEMENT[at0008] occurrences matches {1..1} matches {  -- Preferred name
                        value matches {
                            DV_BOOLEAN matches {*} 
                        }
                    }
                    ELEMENT[at0009] occurrences matches {0..1} matches {  -- Conditional use
                        value matches {
			    DV_TEXT matches {*}
                            DV_CODED_TEXT matches { 
                                defining_code matches {
                                    [local::
                                    at0031,
                                    at0032,
                                    at0033,
                                    at0034,
                                    at0035,
                                    at0036]
                                }
                            }
                        }
                    }
                    ELEMENT[at0010] occurrences matches {0..1} matches {  -- full name
                        value matches {
			    DV_TEXT matches {*}
                        }
                    }
                }
            }
        }
    }




ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Nome da pessoa">
					description = <"Estrutura do nome de uma pessoa.">
				>
				["at0001"] = <
					text = <"Componentes do nome">
					description = <"Componentes do nome da pessoa.">
				>
				["at0002"] = <
					text = <"Nomes atribuídos">
					description = <"Nomes atribuídos de uma pessoa.">
				>
				["at0003"] = <
					text = <"Sobrenomes">
					description = <"Sobrenomes de uma pessoa.">
				>
				["at0004"] = <
					text = <"Títulos do nome">
					description = <"Conjunto de títulos que acompanham o nome de uma pessoa.">
				>
				["at0005"] = <
					text = <"Sufixos do nome">
					description = <"Conjunto de sufixos que acompanham o nome de uma pessoa.">
				>
				["at0006"] = <
					text = <"Grupo de utilização do nome">
					description = <"Grupo que descreve como este nome deve ser usado.">
				>
				["at0007"] = <
					text = <"Representação alternativa do nome">
					description = <"Grupo que descreve uma forma alternativa de representar o nome de uma pessoa.">
				>
				["at0008"] = <
					text = <"Nome preferencial">
					description = <"Indica o nome pelo qual a pessoa prefere ser identificada.">
				>
				["at0009"] = <
					text = <"Indicador de uso condicional">
					description = <"Um indicador das condições específicas ou regras que devem ser aplicadas ao nome de uma pessoa.">
				>
				["at0010"] = <
					text = <"Nome completo">
					description = <"O nome completo da pessoa, resultado da concatenação de suas partes componentes.">
				>
				["at0011"] = <
					text = <"Nome atribuído">
					description = <"O nome que identifica a pessoa no grupo familiar ou pelo qual ela é univocamente identificada socialmente.">
				>
				["at0012"] = <
					text = <"Sobrenome">
					description = <"A parte do nome que uma pessoa possui em comum com outros membros de sua família, distinta do(s) seu(s) nome(s) atribuído(s).">
				>
				["at0014"] = <
					text = <"Título do nome">
					description = <"Uma forma honorífica que inicia o nome, usada ao se referir a uma pessoa pelo nome, seja por correspondência ou por telefone, ou, dependendo da situação cultural, pessoalmente.">
				>
				["at0016"] = <
					text = <"Sufixo do nome">
					description = <"Termo adicional usado após o nome da pessoa.">
				>
				["at0018"] = <
					text = <"Uso do nome">
					description = <"Uma classificação que permite a diferenciação entre os usos do nome de uma pessoa. Um nome de um indivíduo pode ter muitos usos.">
				>
				["at0019"] = <
					text = <"Intervalo de utilização">
					description = <"O período de tempo para o qual este uso do nome se aplica para o nome ao qual ele está associado.">
				>
				["at0020"] = <
					text = <"Identificador usado">
					description = <"A combinação do tipo de identificador, emissor do identificador e nome do identificador que especifica o vínculo entre este nome e o uso do identificador para relatório, ou outros usos.">
				>
				["at0021"] = <
					text = <"Uso da representação">
					description = <"Nome da forma de representação usada.">
				>
				["at0022"] = <
					text = <"Representação alternativa do nome">
					description = <"Representação alternativa do nome desta pessoa usando estilos alternativos de representação, tais como caracteres ou variações do conjunto de caracteres do idioma para exibição local.">
				>
				["at0023"] = <
					text = <"Nome para relatório">
					description = <"O nome do sujeito usado para relatório, quando acompanhado por um identificador específico.">
				>
				["at0024"] = <
					text = <"Nome de recém-nascido">
					description = <"Tipo reservado para identificação de recém-nascidos sem nome.">
				>
				["at0025"] = <
					text = <"Nome profissional ou comercial">
					description = <"O nome usado pela pessoa para propósitos profissionais ou de negócios.">
				>
				["at0026"] = <
					text = <"Nome de solteiro(a)">
					description = <"O nome usado pela pessoa antes de se casar.">
				>
				["at0027"] = <
					text = <"Nome legal">
					description = <"Nome registrado (legal) para a pessoa.">
				>
				["at0028"] = <
					text = <"Outro nome">
					description = <"Qualquer outro nome pelo qual o sujeito é conhecido, ou foi conhecido no passado.">
				>
				["at0031"] = <
					text = <"Nome não confiável">
					description = <"Indica que o nome registrado é um nome fictício ou parcial.">
				>
				["at0032"] = <
					text = <"Escrito de forma errada">
					description = <"Este indicador permite ao usuário indicar que há um erro de digitação, mas que deve ser retido para um uso potencial na vinculação de registros.">
				>
				["at0033"] = <
					text = <"Nome para não ser usado">
					description = <"Indica que este nome não deve ser usado ao se referir à pessoa.">
				>
				["at0034"] = <
					text = <"Vínculo do nome proibido por lei">
					description = <"Indica que este nome e todos os nomes antes dele na seqüência de nomes não devem ser exibidos ou indicados de nenhum modo ao procurar por ou lidar com informações e eventos associados com um nome que segue a este na seqüência de nomes.">
				>
				["at0035"] = <
					text = <"Requisito especial seguranca/privacidade">
					description = <"Indica um nome para o qual episódios são vinculados e devem somente ser acessados por pessoas autorizadas.">
				>
				["at0036"] = <
					text = <"Nome temporário">
					description = <"Indica que o nome da pessoa não foi determinado.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Person name">
					description = <"Person name details.">
				>
				["at0001"] = <
					text = <"Name">
					description = <"Components of a person name.">
				>
				["at0002"] = <
					text = <"Given names">
					description = <"Person given names.">
				>
				["at0003"] = <
					text = <"Family names">
					description = <"Person family names.">
				>
				["at0004"] = <
					text = <"Titles">
					description = <"Person name titles.">
				>
				["at0005"] = <
					text = <"Suffixes">
					description = <"Person name suffixes.">
				>
				["at0006"] = <
					text = <"Name usages">
					description = <"Details of person name usages.">
				>
				["at0007"] = <
					text = <"Alternative name representations">
					description = <"Details of alternative representations such as local codesets or language variations.">
				>
				["at0008"] = <
					text = <"Preferred name">
					description = <"Indicates that this is the name by which a person chooses to be identified.">
				>
				["at0009"] = <
					text = <"Specific issues">
					description = <"Specific issues that apply to the use of the person name.">
				>
				["at0010"] = <
					text = <"Full Name">
					description = <"The person's full name as a concatenation of its name parts.">
				>
				["at0011"] = <
					text = <"Given name">
					description = <"The person's identifying name(s) within the family group or by which he/she is uniquely socially identified.">
				>
				["at0012"] = <
					text = <"Family name">
					description = <"The part of a name a person usually has in common with some other members of his/her family, as distinguished from his/her given names.">
				>
				["at0014"] = <
					text = <"Title">
					description = <"An honorific form of address commencing a name, used when addressing a person by name, whether by mail, by phone, or depending upon cultural situation in person.">
				>
				["at0016"] = <
					text = <"Suffix">
					description = <"Additional term used following a person name.">
				>
				["at0018"] = <
					text = <"Name usage">
					description = <"A classification that enables differentiation between the usage of names for a person. An individual name may have many name uses">
				>
				["at0019"] = <
					text = <"Usage interval">
					description = <"The period of time for which this name usage applies.">
				>
				["at0020"] = <
					text = <"Usage identifier">
					description = <"The combination of identifier type, identifier issuer and identifier name that specify the link between this name and reporting or other unique identifier usage.">
				>
				["at0021"] = <
					text = <"Representation usage">
					description = <"Name of the representational form used.">
				>
				["at0022"] = <
					text = <"Alternative representation">
					description = <"Alternative Representation of this subject of care name using alternative styles of representation such as characters or language character set variations for local display.">
				>
				["at0023"] = <
					text = <"Reporting name">
					description = <"The subject’s name as it is to be used for reporting, when used with a specific identifier.">
				>
				["at0024"] = <
					text = <"Newborn name">
					description = <"A type reserved for the identification of unnamed newborn babies.">
				>
				["at0025"] = <
					text = <"Professional or business name">
					description = <"The name used by the subject for business or professional purposes.">
				>
				["at0026"] = <
					text = <"Maiden name">
					description = <"The name used by the subject of care prior to marriage.">
				>
				["at0027"] = <
					text = <"Legal name">
					description = <"Registered name (Legal name).">
				>
				["at0028"] = <
					text = <"Other name">
					description = <"Any other name by which the subject is known, or has been known by in the past.">
				>
				["at0031"] = <
					text = <"Unreliable information">
					description = <"The name recorded is a fictitious or partial name.">
				>
				["at0032"] = <
					text = <"Known misspelling">
					description = <"This is a misspelling, but should be retained for potential future matching.">
				>
				["at0033"] = <
					text = <"Name not to be used">
					description = <"This name should not be used when referring to this subject.">
				>
				["at0034"] = <
					text = <"Name linkage forbidden by law">
					description = <"By Law, this name and all names prior to it in the name sequence are not to be displayed or indicated in any way when searching for or dealing with information and events associated with a name that is subsequent to this one in the name sequence.">
				>
				["at0035"] = <
					text = <"Special privacy/security requirements">
					description = <"A name for which episodes are attached that should only be accessible to specified authorised persons.">
				>
				["at0036"] = <
					text = <"Temporary name">
					description = <"A temporary name issued where the subject's name has not been determined.">
				>
			>
		>
	>
